apply plugin: "maven-publish"
apply plugin: "org.jreleaser"

publishing {
    publications {
        release(MavenPublication) {
            groupId = "io.github.zhigaras"
            artifactId = project.name

            afterEvaluate {
                from(components["release"])
            }

            pom {
                name = "FireFlow"
                description = "Library for Firebase Realtime Database easy usage"
                url = "https://github.com/Zhigaras/FireFlow"
                inceptionYear = "2026"
                licenses {
                    license {
                        name = "Apache-2.0"
                        url = "https://spdx.org/licenses/Apache-2.0.html"
                    }
                }
                developers {
                    developer {
                        id = "zhigaras"
                        name = "Zhigaras Ilya"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/zhigaras/FireFlow.git"
                    developerConnection = "scm:git:ssh://github.com/zhigaras/FireFlow.git"
                    url = "http://github.com/zhigaras/FireFlow"
                }
            }
        }
    }

    repositories {
        maven {
            name "staging"
            url layout.buildDirectory.dir("staging-deploy")
        }
    }
}

jreleaser {
    environment {
        variables = rootProject.file(".jreleaser/config.toml")
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral.create("sonatype") {
                active = 'ALWAYS'
                url = "https://central.sonatype.com/api/v1/publisher"
                stagingRepository("build/staging-deploy")
                applyMavenCentralRules = false
                sign = true
            }
        }
    }
}

def cleanStaging = tasks.register("cleanStaging", Delete) {
    mkdir layout.buildDirectory.dir('jreleaser')
    delete layout.buildDirectory.dir('staging-deploy')
}

tasks.register("fullCustomPublish") {
    group 'publishing'
    description 'Cleans staging directory, builds artefacts, calls jreleaserFullRelease'

    def buildTask = tasks.named('publishReleasePublicationToStagingRepository').get()
    def publishTask = tasks.named('jreleaserFullRelease').get()

    dependsOn cleanStaging, buildTask, publishTask

    buildTask.configure { mustRunAfter cleanStaging }
    publishTask.configure { mustRunAfter buildTask }
}
